scchart DDDPacemaker {
	input signal ASense, VSense, AVITO, PVARPTO, VRPTO, AEITO, LRITO, URITO;
	output signal APace, VPace;
	output signal AVI_start, PVARP_start, VRP_start, AEI_start, LRI_start, URI_start;
	output signal AVI_stop, AEI_stop, LRI_stop;
	
	initial state top{
	  	signal PVARP_ex, VRP_ex, URI_ex;
	  	
		region AVI:
		initial state intState --> commit1 with APace
					   --> commit1 with ASense;
		connector state commit1 --> waitAVI immediate with /AVI_start;
		state waitAVI "waitAVI"--> commit2 with VSense
							   --> commit2 with VPace
		                	   --> waitURI with AVITO;
		connector state commit2 --> waitPVARP immediate with / AVI_stop; 
		state waitPVARP --> intState with PVARP_ex;
		state waitURI --> intState with URI_ex / VPace;
		
		region PVARP:
		initial state idle --> commit1 with VSense
		 				   --> commit1 with VPace;
		connector state commit1 --> waitPVARP immediate with / PVARP_start;
		state waitPVARP --> idle with PVARPTO / PVARP_ex;
		
		region VRP:
		initial state idle --> commit1 with VSense
						   --> commit1 with VPace;
		connector state commit1 --> waitVRP immediate with / VRP_start;
		state waitVRP --> idle with VRPTO / VRP_ex;
		
		region AEI:
		initial state intState --> commit1 with VSense
							   --> commit1 with VPace;
		connector state commit1 --> waitAEI immediate with / AEI_start;
		state waitAEI "waitAEI"--> commit2 with ASense
							   --> commit2 with APace
		                	   --> intState with AEITO / APace;
		connector state commit2 --> waitVRP immediate with / AEI_stop; 
		state waitVRP --> intState with VRP_ex;
		
		region LRI:
		state waitVRP --> waitLRI with VRP_ex;
		initial state waitLRI "waitLRI"--> commit1 with VSense
		   					   --> commit1 with VPace
		                	   --> intState with LRITO / VPace;
		connector state commit1 --> intState immediate with / LRI_stop;
		state intState --> waitVRP with /LRI_start;
		
		region URI:
		initial state init --> commit1 with VSense
						   --> commit1 with VPace;
		connector state commit1 --> waitURI immediate with / URI_start;
		state waitURI "waitURI" --> init with URITO / URI_ex;
	};
}

