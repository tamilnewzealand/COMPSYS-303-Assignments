scchart DDDPacemaker {
	input signal ASense, VSense, PVARPTO, VRPTO, LRITO;
	output signal APace, VPace;
	
	initial state top{
	  	signal PVARP_start, PVARP_stop, PVARP_ex;
		signal VRP_start, VRP_stop, VRP_ex;
	  	signal LRI_start, LRI_stop, LRI_ex;
	  	
		region PVARP_timer:
		initial state idle --> timer with PVARP_start;
		state timer "timer" --> idle with PVARPTO / PVARP_ex;
		
		region PVARP:
		initial state idle --> commit1 with VSense
		 				   --> commit1 with VPace;
		connector state commit1 --> waitPVARP immediate with / PVARP_start;
		state waitPVARP --> idle with PVARP_ex
		 			    --> commit2 with ASense;
		connector state commit2 --> waitPVARP immediate;
		
	  	region VRP_timer:
		initial state idle --> timer with VRP_start;
		state timer "timer" --> idle with VRPTO / VRP_ex;
		
		region VRP:
		initial state idle --> commit1 with VSense
						   --> commit1 with VPace;
		connector state commit1 --> waitVRP immediate with / VRP_start;
		state waitVRP --> idle with VRP_ex
					  --> commit2 with VSense;
		connector state commit2 --> waitVRP immediate;
		
		region LRI_timer:
		initial state idle --> timer with LRI_start;
		state timer "timer"	 --> idle with LRI_stop
		--> idle with LRITO/ LRI_ex;
		
		region LRI:
		initial state init --> commit1 with VSense;
		connector state commit1 --> waitVRP immediate with / LRI_start;
		state waitVRP --> waitLRI with VRP_ex;
		state waitLRI "waitLRI"--> commit2 with VSense
		                	   --> commit3 with LRI_ex;
		connector state commit2 --> intState immediate with / LRI_stop;
		connector state commit3 --> intState immediate with / VPace;
		state intState --> waitVRP with /LRI_start;
	};
}

